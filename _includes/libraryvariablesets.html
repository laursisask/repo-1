<h1><a id="libraryvariablesets">Configuring shared variables</a></h1>
<p>
    Library variable sets provide a way to share variables across many projects. This allows projects to access a common set of variables capturing cross cutting details such as accounts, AWS regions, and names of CloudFormation stacks that multiple projects query for outputs.
</p>
<p>
    To create the library variable set in Octopus, apply the Terraform configuration in the <strong>terraform/libraryvariablesets/octopus</strong> directory using the following commands:
</p>
<pre>cd terraform/libraryvariablesets/octopus
terraform init
terraform apply</pre>
<p>
    You are prompted for the URL of your Octopus instance, the Octopus API key, and the ID of the space to apply the configuration to. Terraform then displays a list of changes and prompts you to apply them. Once the configuration is applied a new library variable set called <strong>Octopub</strong> containing three variables is created:
</p>
<ul>
    <li><strong>AWS.Account</strong> - A reference to the account created previously.</li>
    <li><strong>AWS.CloudFormation.ApiGatewayStack</strong> - The name of the CloudFormation stack that will create the shared API Gateway.</li>
    <li><strong>AWS.Region</strong> - The AWS region that the resources are created in.</li>
</ul>
<div><img alt="Octopus environments" src="images/octopus/libraryvariableset.png"/></div>
<h2>Breaking down the HCL</h2>
<p>
  A common requirement as we introduce new resources into existing spaces is to reference the ID of existing resources. This is done with <strong>data</strong> lookups.
</p>
<p>
  Here we look up the ID of the AWS account created in a previous chapter:
</p>
<pre>data "octopusdeploy_accounts" "aws_account" {
    account_type = "AmazonWebServicesAccount"
    ids          = []
    partial_name = "AWS Account"
    skip         = 0
    take         = 1
  }</pre>
<p>
    Here we create the library variable set:
</p>
<pre>resource "octopusdeploy_library_variable_set" "library_variable_set_octopub" {
    name        = "Octopub"
    description = ""
  }</pre>
<p>
    Octopus variables can either belong to a project or a library variable set. Both are represented by the same resource in HCL. The difference is the resource that is set as the variable owner.
</p>
<p>
    Here we create an Octopus variable that references the AWS account retrieved in the data lookup above and assign it to the library variable set:
</p>
<pre>resource "octopusdeploy_variable" "library_variable_set_octopub_aws_account_0" {
    owner_id     = "${octopusdeploy_library_variable_set.library_variable_set_octopub.id}"
    value        = "${data.octopusdeploy_accounts.aws_account.accounts[0].id}"
    name         = "AWS.Account"
    type         = "AmazonWebServicesAccount"
    is_sensitive = false
  
    scope {
      actions      = []
      channels     = []
      environments = []
      machines     = []
      roles        = null
      tenant_tags  = null
    }
    depends_on = []
  }</pre>
<p>
    Here we create a Terraform variable to expose the AWS region our cloud resources are created in:
</p>
<pre>variable "library_variable_set_octopub_aws_region_1" {
    type        = string
    nullable    = false
    sensitive   = false
    description = "The value associated with the variable AWS.Region"
    default     = "ap-southeast-2"
  }</pre>
<p>
    The Terraform variable is then captured in an Octopus variable:
</p>  
<pre>resource "octopusdeploy_variable" "library_variable_set_octopub_aws_region_1" {
    owner_id     = "${octopusdeploy_library_variable_set.library_variable_set_octopub.id}"
    value        = "${var.library_variable_set_octopub_aws_region_1}"
    name         = "AWS.Region"
    type         = "String"
    description  = ""
    is_sensitive = false
  
    scope {
      actions      = []
      channels     = []
      environments = []
      machines     = []
      roles        = null
      tenant_tags  = null
    }
    depends_on = []
  }</pre>
<p>
    We follow the pattern of creating a Terraform variable and capturing it in an Octopus variable here with the name of the CloudFormation stack used to create the shared API Gateway:
</p>
<pre>variable "library_variable_set_octopub_aws_cloudformation_apigatewaystack_0" {
    type        = string
    nullable    = false
    sensitive   = false
    description = "The value associated with the variable AWS.CloudFormation.ApiGatewayStack"
    default     = "OctopubApiGateway-#{Octopus.Environment.Name}"
  }
  
  resource "octopusdeploy_variable" "library_variable_set_octopub_aws_cloudformation_apigatewaystack_0" {
    owner_id     = "${octopusdeploy_library_variable_set.library_variable_set_octopub.id}"
    value        = "${var.library_variable_set_octopub_aws_cloudformation_apigatewaystack_0}"
    name         = "AWS.CloudFormation.ApiGatewayStack"
    type         = "String"
    description  = ""
    is_sensitive = false
  
    scope {
      actions      = []
      channels     = []
      environments = []
      machines     = []
      roles        = null
      tenant_tags  = null
    }
    depends_on = []
  }</pre>
<p>
    We now have all the global Octopus resources required to begin deploying AWS resources. The next step is to deploy a shared API Gateway instance.
</p>
